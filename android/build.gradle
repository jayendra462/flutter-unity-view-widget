group 'com.xraph.plugin.flutter_unity_widget'
version '4.0-SNAPSHOT'

buildscript {
    ext.kotlin_version = '2.0.20'
    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:8.7.3'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

rootProject.allprojects {
    repositories {
        google()
        mavenCentral()

        // DO NOT MODIFY
        // BUILD_ADD_UNITY_LIBS

        // FOR DEV ONLY
        flatDir {
            dirs "libs"
        }
    }
}

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'

android {
    compileSdkVersion 35

    // backwards compatible for old gradle versions without namespace
    if (project.android.hasProperty("namespace")) {
        namespace 'com.xraph.plugin.flutter_unity_widget'
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }
    defaultConfig {
        minSdkVersion 29
    }
    lintOptions {
        disable 'InvalidPackage'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_17
    }
}

def lifecycle_version = '2.8.7'
def annotation_version = '1.9.1'

dependencies {
    implementation project(':unityLibrary')
    implementation(name: 'unity-classes', ext:'jar')

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation "androidx.lifecycle:lifecycle-common:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-common-java8:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-process:$lifecycle_version"
    implementation "androidx.annotation:annotation:$annotation_version"

    compileOnly rootProject.findProject(":flutter_plugin_android_lifecycle")

    // FOR DEV ONLY
//    implementation(name: 'flutter', ext:'jar')
}
